apply plugin: 'com.android.application'

android {
    namespace 'com.flightcamera.app'
    compileSdkVersion 29
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileOptions.encoding = 'UTF-8'

    defaultConfig {
        applicationId "com.flightcamera.app"
        minSdkVersion 19
        // Important! When we decide to change this to 30 or more, we need to add full support for scoped storage
        targetSdkVersion 29

        renderscriptTargetApi 21
        //renderscriptSupportModeEnabled true // don't use support library as it bloats the APK, and we don't need pre-4.4 support
        // need build tools at least 20 at least to support ScriptIntrinsicHistogram
        // also need build tools at least 21 to avoid Google Play 64-bit warning (required from 1 August 2019)

        testApplicationId "com.flightcamera.app.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {
        release
        if (System.getenv("CI")) {
            release {
                storeFile rootProject.file("release.jks")
                storePassword System.getenv("keystore_password")
                keyAlias System.getenv("keystore_alias")
                keyPassword System.getenv("keystore_alias_password")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                outputs.upToDateWhen {false}
                exceptionFormat "full"
                events "skipped", "passed", "failed"
                showStandardStreams true
            }
        }
    }
    // needed to use android.test package (ActivityInstrumentationTestCase2 etc) when targetting sdk 28 (Android 9) -
    // see https://developer.android.com/training/testing/set-up-project
    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    buildToolsVersion '28.0.3'
    //useLibrary 'android.test.mock'
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.1'
    testImplementation 'junit:junit:4.13.1'
}
